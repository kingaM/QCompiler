import java_cup.runtime.*; 
import ast.*;
import java.util.ArrayList;
import java.util.Hashtable;
  
parser code {:
public static String section = "Main Body Declaration"; 
public static int numOfErrors = 0;
public static int numOfMains = 0;

public void report_fatal_error(String message, Object info) {
    report_error(message, info);
    System.exit(1);
}

public void syntax_error(Symbol cur_token) {
    numOfErrors++;
    report_error("Syntax error at line " + (cur_token.left+1) +
   ", column " + (cur_token.right+1) + " at section " + QCup.section , cur_token);
}

:} 
 
terminal        String ID, TYPE, VOID;
terminal		Boolean BOOL;
terminal 		Float FLOAT;
terminal 		Integer INT;
terminal 		String STRING_LITERAL;
terminal		Character CHAR;

terminal 		EQ, NOTEQ, EQCOMP, LESS, LESSOREQ, GREATER, GREATEROREQ, PLUS, MINUS, TIMES, DIVIDE, POWER,
                NOT, OR, AND, SEMI, COLON, LPAREN, RPAREN, LBRACE, RBRACE, LSQPAREN, RSQPAREN, COMMA, LEN, DOT,
                TDEF, FDEF, RETURN, IN, IF, ELSE, DO, WHILE, REPEAT, UNTIL, CONCAT, LTUPLE, RTUPLE;

non terminal    Program program;
non terminal 	ArrayList<Decl> declList;
non terminal 	String typeSpecifier;
non terminal 	Decl functionDecl, functionDeclVoid, functionDeclReturn, variableDecl, typeDecl;
non terminal 	MainDecl mainDecl;
non terminal 	ArrayList<Field> fieldDecl, fieldDeclOptional;
non terminal 	ArrayList<Stmt> funcStmtList, funcBlockStmt,mainStmtList, mainBlockStmt;
non terminal 	Stmt returnStmt, funcStmt, funcStmtNoFollowingSubstmt, mainStmt, mainStmtNoFollowingSubstmt;

non terminal 	Expr expr, fcall, value;
non terminal 	ArrayList<Expr> sequence, sequenceOptional;


precedence nonassoc EQ, NOTEQ;
precedence left OR, AND;
precedence left EQCOMP,LESS, LESSOREQ, GREATER, GREATEROREQ, IN;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence right POWER;
precedence left NOT;
precedence left LPAREN, RPAREN;
precedence left ELSE, CONCAT, DOT;


program             ::= declList:l mainDecl:m
	                    {: 
	                    //AST
	                    System.out.println(m);
	                    RESULT = new Program(l, m);
	                    
	                    //Errors and info
	                    QCup.section = "Main Declaration"; 
	                    if(QCup.numOfErrors == 0) System.out.println("Parsing successful");
	                    else if(QCup.numOfErrors == 1) System.out.println("Parsing unsuccessful, you had " + QCup.numOfErrors + " error.");
	                    else System.out.println("Parsing unsuccessful, you had " + QCup.numOfErrors + " errors."); 
	                    if(parser.numOfMains > 1) System.err.println("You declared " + parser.numOfMains + " main programs. Only one main is accepted."); 
	                    
	                    :}
	                    | declList error {: System.err.println("No main declaration at the end of file"); :}
	                    ; 

declList             ::= declList:d mainDecl
						{: 
						//AST
						RESULT = d;
						//Error checking
						QCup.section = "Main Body Declaration"; :}
                        | declList:d typeDecl:t
                        {: 
                        //AST
						d.add(t);
						RESULT = d;
						//Error checking
                        QCup.section = "Type Declaration"; :}
                        | declList:d variableDecl:v
                        {: 
                        //AST
						d.add(v);
						RESULT = d;
						//Error checking
                        QCup.section = "Variable Declaration"; :} 
                        | declList:d functionDecl:f
                        {: 
                        //AST
						d.add(f);
						RESULT = d;
						//Error checking
                        QCup.section = "Function Declaration"; :}
                        | 
                        {: RESULT = new ArrayList<Decl>(); :}
                        | declList error RBRACE {: System.err.println("Invalid function definition"); :}
                        ;
                    
typeDecl            ::= TDEF ID:id COLON fieldDecl:f SEMI
						{:
						//AST
						RESULT = new TypeDecl(id, f);
						:}
                        | TDEF error SEMI {: System.err.println("Invalid type declaration"); :}
                        ;
                    
variableDecl        ::= ID:id COLON typeSpecifier:type SEMI
						{:
						RESULT = new VariableDecl(id, type, null);
						:}
                        | ID:id COLON typeSpecifier:type EQ sequence:seq SEMI 
                        {:
						RESULT = new VariableDecl(id, type, seq);
						:}
                        | ID COLON error SEMI {: System.err.println("Invalid variable declaration"); :}
                        ;


functionDecl        ::= functionDeclVoid:f
						{: RESULT = f; :}
                        | functionDeclReturn:f   
                        {: RESULT = f; :}                     
                        | FDEF error RBRACE {: System.err.println("Invalid function definition or missing return statement"); :}
                        ;
                    

functionDeclReturn  ::= FDEF ID:id LPAREN fieldDeclOptional:fieldDecl RPAREN COLON typeSpecifier:type LBRACE funcStmtList:stmts RBRACE
						{: 
						RESULT = new FunctionDecl(id, type, fieldDecl, stmts); :}
                        ;
                        

functionDeclVoid    ::= FDEF ID:id LPAREN fieldDeclOptional:fieldDecl RPAREN COLON VOID LBRACE funcStmtList:stmts RBRACE
						{: RESULT = new FunctionDecl(id, "void", fieldDecl, stmts); :}
                        ;


mainDecl     		::= LBRACE mainStmtList:s RBRACE 
                        {: 
                        //AST
                        RESULT = new MainDecl(s);
                        
                        //Error
                        if(parser.numOfMains > 1){ parser.numOfErrors ++; }
                               else parser.numOfMains ++; :}
                        | LBRACE error RBRACE {: System.err.println("Invalid main body"); :}
                        ;

 
fieldDeclOptional   ::= fieldDecl
						|
                        ;
                                  

fieldDecl           ::= fieldDecl:f COMMA ID:id COLON typeSpecifier:type
						{: 
						f.add(new Field(id, type));
						RESULT = f;
						:} 
                        | ID:id COLON typeSpecifier:type
                        {:
                        ArrayList<Field> a = new ArrayList<Field>();
                        a.add(new Field(id, type));
                        RESULT = a;
                        :}
                        ;
                    
sequence            ::= sequence:s COMMA expr:e
						{:
						s.add(e);
						RESULT = s;
						:} 
                        | expr:e
						{: 
						ArrayList<Expr> a = new ArrayList<Expr>();
						a.add(e);
                        RESULT = a;
                        :}
                        ; 
        
typeSpecifier      	::= TYPE:t
						{: RESULT = t; :}
                        | ID:id
                        {: RESULT = id; :}
                        ;

mainStmtList        ::= mainStmtList:msl variableDecl:vd
						{: 
						msl.add(new VarStmt(vd));
						  RESULT = msl;
						:}
						| mainStmtList:msl mainStmt:ms
						{:
						msl.add(ms);							 
						RESULT = msl;
						:}
                        |
                        {:
                    	RESULT = new ArrayList<Stmt>();
                    	:}
                        ;
                        
                        
funcStmtList		::= funcStmtList:fsl variableDecl:vd
						{: 
						  fsl.add(new VarStmt(vd));
						  RESULT = fsl;
						:}
                     	| funcStmtList:fsl funcStmt:fs
                     	{:
						fsl.add(fs);							 
						RESULT = fsl;
						:}
                    	|
                    	{:
                    	RESULT = new ArrayList<Stmt>();
                    	:}
                     	;

sequenceOptional  	::= sequence:seq
						{: RESULT = seq; :}
                        |
                        ; 
                        
                        
fcall               ::= ID:id LPAREN sequenceOptional:seq RPAREN
						{: RESULT = new FcallExpr(id, seq); :}
                        ;
 
                
mainStmt           	::= mainStmtNoFollowingSubstmt:s
						{: RESULT = s; :}
                        | IF LPAREN expr:e RPAREN mainBlockStmt:b
                        {: RESULT = new IfStmt(e, b); :}
                        | IF error RBRACE {: System.err.println("Invalid if statement"); :}
                        | IF LPAREN expr:e RPAREN mainBlockStmt:ib ELSE mainBlockStmt:eb                        
                        {: RESULT = new IfElseStmt(e, ib, eb); :}
                        | WHILE LPAREN expr:e RPAREN DO mainBlockStmt:b                          
                        {: RESULT = new WhileStmt(e, b); :}
                        | WHILE error RBRACE {: System.err.println("Invalid while statement"); :} 
                        ;
                        
                  
funcStmt          	::= funcStmtNoFollowingSubstmt:s
						{: RESULT = s; :}
                        | IF LPAREN expr:e RPAREN funcBlockStmt:b
                        {: RESULT = new IfStmt(e, b); :}
                        | IF error RBRACE {: System.err.println("Invalid if statement"); :}
                        | IF LPAREN expr:e RPAREN funcBlockStmt:ib ELSE funcBlockStmt:eb
                        {: RESULT = new IfElseStmt(e, ib, eb); :}
                        | WHILE LPAREN expr:e RPAREN DO funcBlockStmt:b 
                        {: RESULT = new WhileStmt(e, b); :}
                        | WHILE error RBRACE {: System.err.println("Invalid while statement"); :} 
                        ;
                  
funcBlockStmt      	::= LBRACE funcStmtList:fs RBRACE
						{: ArrayList<Stmt> a = new ArrayList<Stmt>();
						   a = fs;
						   RESULT = a;
					    :}
                        | error SEMI {: System.err.println("Braces are neccessary"); :} 
                        ; 
                        
mainBlockStmt     	::= LBRACE mainStmtList:ms RBRACE
						{: ArrayList<Stmt> a = new ArrayList<Stmt>();
						   a = ms;
						   RESULT = a;
					    :}
                        | error SEMI {: System.err.println("Braces are neccessary"); :} 
                        ;
                      
funcStmtNoFollowingSubstmt 		::= SEMI
                                    | returnStmt:r 
                                    {: RESULT = r; :}
                                    | REPEAT funcBlockStmt:fb UNTIL LPAREN expr:e RPAREN
                                    {: RESULT = new RepeatUntilStmt(e, fb); :}
                                    | expr:e SEMI
                                    {: RESULT = new ExprStmt(e); :}
                                    | REPEAT error SEMI
                                    ; 
     
mainStmtNoFollowingSubstmt 		::= SEMI
                                    | REPEAT mainBlockStmt:mb UNTIL LPAREN expr:e RPAREN
                                    {: RESULT = new RepeatUntilStmt(e, mb); :}
                                    | expr:e SEMI
                                    {: RESULT = new ExprStmt(e); :}
                                    | REPEAT error SEMI
                                    ;     
                               
returnStmt    		 ::= RETURN SEMI 
                        | RETURN expr:e SEMI
                        {: RESULT = new ReturnStmt(e); :}  
                        ;
  
expr				 ::= ID: id
						{: RESULT = new VarExpr(id); :}
                        | value:v
                        {: RESULT = v; :}
                        | fcall:f
                        {: RESULT = f; :}
                        | expr:l DOT expr:r
                        {: RESULT = new DotBinaryExpr(l, r); :}
                        | expr:l EQ expr:r 
                        {: RESULT = new EqBinaryExpr(l, r); :}
                        | expr:l CONCAT expr:r
                        {: RESULT = new ConcatBinaryExpr(l, r); :}
                        | LEN LPAREN expr:seq RPAREN 
                        {: ArrayList<Expr> a = new ArrayList<Expr>();
                        a.add(seq);
                        RESULT = new FcallExpr("len", a); :}
                        | expr:l LSQPAREN expr:r RSQPAREN
                        {: RESULT = new SeqCallExpr(l, r); :}
                        | expr:seq LSQPAREN expr:s COLON expr:f RSQPAREN
                        {: RESULT = new SeqSlicingExpr(seq, s, f); :}
                        | expr:seq LSQPAREN COLON expr:f RSQPAREN
                        {: RESULT = new SeqSlicingExpr(seq, null, f); :}
                        | expr:seq LSQPAREN expr:s COLON RSQPAREN
                        {: RESULT = new SeqSlicingExpr(seq, s, null); :}
                        | expr:l PLUS expr:r
                        {: RESULT = new PlusBinaryExpr(l, r); :}
                        | expr:l MINUS expr:r
                        {: RESULT = new MinusBinaryExpr(l, r); :}
                        | expr:l TIMES expr:r
                        {: RESULT = new TimesBinaryExpr(l, r); :}
                        | expr:l DIVIDE expr:r
                        {: RESULT = new DivideBinaryExpr(l, r); :}
                        | expr:l POWER expr :r
                        {: RESULT = new PowerBinaryExpr(l, r); :}
                        | expr:l EQCOMP expr:r
                        {: RESULT = new EqCompBinaryExpr(l, r); :}
                        | expr:l NOTEQ expr:r
                        {: RESULT = new NotEqCompBinaryExpr(l, r); :}
                        | expr:l LESS expr:r
                        {: RESULT = new LessCompBinaryExpr(l, r); :}
                        | expr:l LESSOREQ expr:r
                        {: RESULT = new LessEqCompBinaryExpr(l, r); :}
                        | expr:l GREATER expr:r
                        {: RESULT = new GreaterCompBinaryExpr(l, r); :}
                        | expr:l GREATEROREQ expr:r 
                        {: RESULT = new GreaterOrEqExpr(l, r); :}
                        | expr:l OR expr:r
                        {: RESULT = new OrExpr(l, r); :}
                        | expr:l AND expr:r
                        {: RESULT = new AndExpr(l, r); :}
                        | NOT expr:e
                        {: RESULT = new NotUnaryExpr(e); :}
                        | expr:l IN expr:r
                        {: RESULT = new InExpr(l, r); :}
                        | LPAREN expr:e RPAREN 
                        {: RESULT = e; :}
                        | LTUPLE sequenceOptional:s RTUPLE
                        {: RESULT = new SeqExpr(s, "tuple"); :}
                        | LSQPAREN sequenceOptional:s RSQPAREN
                        {: RESULT = new SeqExpr(s, "list"); :}
                        | INT INT {: parser.numOfErrors++; System.err.println("Cannot have two adjacent numbers with no operator in between.\n"); :} 
                        | ID error SEMI {: System.err.println("Invalid expression"); :} 
                        | value error SEMI {: System.err.println("Invalid expression"); :} 
                        ;

value 				::= BOOL:b
						{: RESULT = new BoolValueExpr(b); :}
                        | INT:i
                        {: RESULT = new IntValueExpr(i); :}
                        | FLOAT:f
                        {: RESULT = new FloatValueExpr(f); :}
                        | STRING_LITERAL:s
                        {: RESULT = new StrValueExpr(s); :}
                        | CHAR:c
                        {: RESULT = new CharValueExpr(c); :}
                        ;
